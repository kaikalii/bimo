WHITESPACE = _{ " " | "\t" | "\r" }
ws = { WHITESPACE }
single_line_comment = @{ "--" ~ (!NEWLINE ~ ANY)* ~ &(NEWLINE | EOI) }
multi_line_comment = @{ "'''" ~ (!"'''" ~ ANY)* ~ ("'''" | &EOI) }
COMMENT = _{ multi_line_comment | single_line_comment }
keyword = { "and" | "or" | "if" | "then" | "else" | "match" }

// Numbers
int = @{ ("+" | "-")? ~ ASCII_DIGIT+ }
real = @{ int ~ ("." ~ ASCII_DIGIT+) ~ (^"e" ~ int)? }

// String literals
string = { (either_string ~ NEWLINE?)+ }
either_string = _{ quoted_string | format_string }
format_string = ${ "$\"" ~ ( format_arg | formatted_string_part)* ~ "\"" }
quoted_string = ${ "\"" ~ string_part* ~ "\"" }
format_arg = !{ "{" ~ expr ~ "}" }
string_part = _{ string_text | "\\" ~ (predefined | byte | unicode) }
formatted_string_part = _{ format_string_text | "\\" ~ (predefined | byte | unicode) | escaped_left_curly | escaped_right_curly }
escaped_left_curly = { "{{" }
escaped_right_curly = { "}}" }
string_text = @{ (!( "\\" | "\"" ) ~ ANY)+ }
format_string_text = @{ (!( "\\" | "\"" | "{" | "}" ) ~ ANY)+ }
hex = _{ '0'..'9' | 'i'..'f' | 'i'..'F' }
unicode_hex = @{ hex{1, 6} }
predefined = { "n" | "r" | "t" | "\\" | "0" | "\"" | "'" }
byte = @{ "x" ~ hex{2} }
unicode = @{ "u" ~ "{" ~ unicode_hex ~ "}" }

// Idents
ident_init = _{ LETTER | OTHER_SYMBOL | "_" }
ident_tail = _{ ident_init | DECIMAL_NUMBER | OTHER_NUMBER }
ident_inner = @{ ident_init ~ ident_tail* }
starts_with_keyword = @{ keyword ~ ("_"* ~ ident_tail+)+ }
ident = { !keyword ~ ident_inner | starts_with_keyword | "_" }

// Container literals
list_literal = { "[" ~ (expr ~ ","?)* ~ "]" }
entity_item = { tag_literal | expr ~ "=>" ~ expr | ident ~ (":" ~ expr)? }
entity_literal = { "{" ~ NEWLINE? ~ (entity_item ~ ","? ~ NEWLINE?)* ~ (".." ~ expr)? ~ "}" }

// Operators
op_as = { "+" | "-" }
op_mdr = { "*" | "/" | "%" }
op_comp = { "==" | "!=" | "<=" | ">=" | "<" | ">" }
op_and = { "and" }
op_or = { "or" }
op_neg = { "-" }

// Terms
paren_expr = { "(" ~ NEWLINE* ~ items ~ ")" }
param = { rebindable_pattern }
closure_params = { (param | "|" ~ (param ~ ","?)*) ~ "|" }
closure = { closure_params ~ NEWLINE? ~ expr }
tag_literal = ${ "#" ~ ident }
pattern_literal = ${ "?" ~ pattern | regex }
term = { real | int | closure | ident | tag_literal | string | paren_expr | list_literal | entity_literal | pattern_literal }

// Access
call_args = { "(" ~ NEWLINE? ~ (expr ~ ","? ~ NEWLINE?)* ~ ")" }
field_access = { NEWLINE? ~ "." ~ ident }
method_call = { NEWLINE? ~ ":" ~ ident ~ call_args }
accessor = { method_call | field_access | call_args }

// Expressions
expr_access = { term ~ accessor* }
expr_neg = { op_neg? ~ expr_access }
expr_mdr = { expr_neg ~ (op_mdr ~ expr_neg)* }
expr_as = { expr_mdr ~ (op_as ~ expr_mdr)* }
expr_cmp = { expr_as ~ (op_comp ~ expr_as)* }
expr_bind = { rebindable_pattern ~ "=" ~ NEWLINE? ~ (expr_cmp | expr_match | expr_if) | expr_cmp }
expr_and = { expr_bind ~ (NEWLINE? ~ op_and ~ NEWLINE? ~ expr_bind)* }
expr_or = { expr_and ~ (NEWLINE? ~ op_or ~ NEWLINE? ~ expr_and)* }
expr_if = { "if" ~ expr ~ "then"? ~ NEWLINE? ~ expr ~ NEWLINE? ~ "else" ~ NEWLINE? ~ expr }
expr_match = { "match" ~ expr ~ NEWLINE ~ (rebindable_pattern ~ "=>" ~ NEWLINE? ~ expr ~ NEWLINE)+ }
expr = { expr_if | expr_match | expr_or }

// Patterns
list_pattern = { "[" ~ (pattern ~ ","?)* ~ "]" }
field_pattern = !{ ident ~ (":" ~ pattern)? }
entity_pattern = { "{" ~ (field_pattern ~ ","?)* ~ "}" }
regex = { "#" ~ either_string }
pattern = !{ ident | int | string | regex | list_pattern | entity_pattern }
rebindable_pattern = { pattern ~ (":" ~ pattern)? }

// Items
function_def = { ident ~ "(" ~ (param ~ ","?)* ~ ")" ~ "=" ~ NEWLINE? ~ expr }
item = { function_def | expr }
items = { (item ~ NEWLINE*)* }
file = { SOI ~ NEWLINE* ~ items ~ EOI }
